apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // Use experimental kapt implementation
apply plugin: 'io.fabric'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "io.prodrink"
        minSdkVersion 21
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ext.betaDistributionGroupAliases = "beta-android"
    }
    signingConfigs {
        release {
            storeFile file("../keystore.jks")
        }
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            ext.betaDistributionNotifications = false
            signingConfig = signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

buildscript {
    ext.supportVersion = '26.0.2'
    ext.retrofitVersion = '2.3.0'
    ext.rxBindingVersion = '2.0.0'
    ext.nucleusVersion = '3.0.0'
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Google Support Libraries
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:support-annotations:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'com.google.android.gms:play-services-vision:11.2.0'

    implementation  'eu.davidea:flexible-adapter:5.0.0-rc2'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3'
    implementation 'com.github.pwittchen:reactivenetwork:0.11.0'

    // Network client
    implementation "com.squareup.okhttp3:okhttp:3.8.1"
    implementation 'com.squareup.okio:okio:1.13.0'

    // REST
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    // JSON
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    // Model View Presenter
    implementation "info.android15.nucleus:nucleus:$nucleusVersion"
    implementation "info.android15.nucleus:nucleus-support-v7:$nucleusVersion"

    // Dependency injection
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'

    // Image library
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.0.0@aar'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Conductor
    implementation "com.bluelinelabs:conductor:2.1.4"

    // RxBindings
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rxBindingVersion"

    // Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:1.7.1'
    testImplementation 'org.mockito:mockito-core:1.10.19'
}

afterEvaluate {
    initFabricPropertiesIfNeeded()
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsApiSecret)
            entry(key: "apiKey", value: crashlyticsApiKey)
        }
    }
}

android.signingConfigs.release.storePassword = System.getenv("storepass")
android.signingConfigs.release.keyAlias = System.getenv("keyalias")
android.signingConfigs.release.keyPassword = System.getenv("keypass")

def versionMajor = 0
def versionMinor = 1
def versionBuild = Integer.parseInt(System.getenv("TRAVIS_BUILD_NUMBER") ?: "0")

android.defaultConfig.versionCode versionBuild
android.defaultConfig.versionName "${versionMajor}.${versionMinor}.${versionBuild}"
